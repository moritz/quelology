#!/usr/bin/env perl
use 5.012;
use warnings;

use Mojolicious::Lite;
use lib 'lib';
use lib '../lib';
use XFacts::Config qw(schema);

my $schema = schema;

get '/' =>  sub {
    my $self = shift;
    my $t = '';
    $self->render(
        threads     => [$schema->m->threads],
        singles     => [$schema->m->singles],
        template    => 'index',
        title       => 'List of series',
    );
} => 'index';

get '/m/:id' => sub {
    my $self = shift;
    my $node = $schema->m->find($self->param('id'));
    my $template = $node->is_leaf ? 'medium' : 'tree';
    $self->render(
        layout  => 'basic',
        title   => 'Details',
        node    => $node,
        template => $template,
    );
} => 'medium';

get '/asin/:asin' => sub {
    my $self = shift;
    my $node = eval {
        $schema->m->from_asin($self->stash('asin'));
    };
    if ($node) {
        $self->render(
            template => 'medium',
            title    => 'Details',
            node     => $node,
            layout   => 'basic',
        );
    } else {
        $self->render(status => 404, text => 'Book not found');
    }

} => 'asin';

get '/t/:id' => sub {
    my $self = shift;
    $self->render(
        layout  => 'basic',
        title   => 'Tree details',
        node    => $schema->m->find($self->param('id')),
    );
} => 'tree';

$SIG{INT} = sub {
    $schema->storage->disconnect;
    exec $^X, $0, @ARGV;
};

app->start;
$schema->storage->disconnect;

__DATA__

@@ index.html.ep
% layout 'basic';
<h1>List of Series</h1>
<ul>
<% for my $n (@$threads) { %>
    <li>
    <%= link_to $n->title, medium => { id => $n->id } %>
    by <strong><%= $n->made_by %></strong>
    </li>
<% } %>
</ul>
<h1>List of single nodes</h1>
<ul>
<% for my $n (@$singles) { %>
    <li>
    <%= link_to $n->title, medium => { id => $n->id } %>
    by <strong><%= $n->made_by %></strong>
    </li>
<% } %>
</ul>

@@ tree.html.ep
<h1><%= $node->title %></h1>
<p>by <strong><%= $node->made_by; %></strong>, published by <strong><%= $node->publisher; %></strong>.</p> 
<ul>
<% my $prev_level = 1;
   for my $n ($node->descendants) { %>
      <%
      my $l = $n->level - $prev_level; 
      $prev_level = $n->level;
      my $ld = '';
      if ($l > 0) {
        $ld = '<ul>' x $l;
      } elsif ($l < 0) {
        $ld = '</ul>' x abs($l);
      }
    %>
        <%== $ld =%>
    <li>
        <% if ($n->small_image) { %>
            <img src="<%= $n->small_image %>" alt="" />
        <% } %>
        <%= link_to($n->title, medium => { id => $n->id }) %>
        by <%= $n->made_by %>
    </li>
<% } =%>
<%== '</ul>' x $prev_level =%>

@@ medium.html.ep
<div class="node level<%= $node->level %>">
    <h2><%= $node->title %></h2>
    <% if (defined $node->made_by) { %>
        <p>Author: <%= $node->made_by %></p>
    <% } %>
    <p>
        <% if (defined $node->medium_image) { %>
            <% if (defined $node->large_image) { %>
                <a href="<%= $node->large_image %>">
            <% } %>
                <img src="<%= $node->medium_image %>"
                    alt="Cover of <%= $node->title %>" />
            <% if (defined $node->large_image) { %>
                </a>
            <% } %>
        <% } %>
        <br />
        <a href="<%= $node->amazon_url %>">
            View on amazon US
        </a>
    </p>
</div>

@@ layouts/basic.html.ep
<!doctype html><html>
    <head>
        <title><%= $title %></title>
        <style type="text/css"><!--
            .node {
            border: 1px dotted black;
            padding: 0.3em;
            max-width: 20em;
        }
  // --></style>
    </head>
    <body><%== content %></body>
</html>
