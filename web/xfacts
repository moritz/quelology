#!/usr/bin/env perl
use 5.012;
use warnings;

use Mojolicious::Lite;
use lib 'lib';
use lib '../lib';
use XFacts::Config qw(schema config);
use utf8;

my $schema = schema;
my $static = Mojolicious::Static->new;

app->secret( config()->{web_secret} );

get '/' =>  sub {
    my $self = shift;
    my $t = '';
    $self->render(
        threads     => [$schema->m->threads],
        singles     => [$schema->m->singles],
        template    => 'index',
        title       => 'List of series',
    );
} => 'index';

get '/m/:id' => sub {
    my $self = shift;
    my $node = $schema->m->find($self->param('id'));
    my $template = $node->is_leaf ? 'medium' : 'tree';
    $self->render(
        layout  => 'basic',
        title   => 'Details',
        node    => $node,
        template => $template,
    );
} => 'medium';

get '/asin/:asin' => sub {
    my $self = shift;
    my $node = eval {
        $schema->m->from_asin($self->stash('asin'));
    };
    if ($node) {
        $self->render(
            template => 'medium',
            title    => 'Details',
            node     => $node,
            layout   => 'basic',
        );
    } else {
        $self->render(status => 404, text => 'Book not found');
    }

} => 'asin';

get '/drop/:id' => sub {
    my $self = shift;
    $self->render(template => 'drop', nodes => [$schema->m
        ->find($self->param('id'))->thread_with_drop_points],
        title => 'foo',
    );
};

get '/t/:id' => sub {
    my $self = shift;
    $self->render(
        layout  => 'basic',
        title   => 'Tree details',
        node    => $schema->m->find($self->param('id')),
    );
} => 'tree';

$SIG{INT} = sub {
    $schema->storage->disconnect;
    exec $^X, $0, @ARGV;
};

app->start;
$schema->storage->disconnect;
1;

__DATA__

@@ index.html.ep
% layout 'basic';
<h1>List of Series</h1>
<ul>
<% for my $n (@$threads) { %>
    <li>
    <%= link_to $n->title, medium => { id => $n->id } %>
    by <strong><%= $n->made_by %></strong>
    </li>
<% } %>
</ul>
<h1>List of single nodes</h1>
<ul>
<% for my $n (@$singles) { %>
    <li>
    <%= link_to $n->title, medium => { id => $n->id } %>
    by <strong><%= $n->made_by %></strong>
    </li>
<% } %>
</ul>

@@ tree.html.ep
% layout 'basic';
<h1><%= $node->title %></h1>
<p>by <strong><%= $node->made_by; %></strong>, published by <strong><%= $node->publisher; %></strong>.</p> 
<% if ($node->id != $node->root_id) { %>
    <p>This is part of a <%= link_to "Bigger series (" . $node->title . ").", tree => { id => $node->root_id }; %></p>
<% } %>
<% if ($node->asin) { %>
    <%= include 'small_medium', n => $node =%>
<% } %>
<ul>
<% my $prev_level = 1;
   for my $n ($node->descendants) {
      my $l = $n->level - $prev_level; 
      $prev_level = $n->level;
      my $ld = '';
      if ($l > 0) {
        $ld = '<ul>' x $l;
      } elsif ($l < 0) {
        $ld = '</ul>' x abs($l);
      }
    %>
        <%== $ld =%>
    <li>
        <%= include 'small_medium', n => $n =%>
    </li>
<% } =%>
<%== '</ul>' x $prev_level =%>


@@ drop.html.ep
% layout 'basic';
<%
    my $first_node = $nodes->[1][0];
%>
<h1><%= $first_node->title %></h1>
<p>by <strong><%= $first_node->made_by; %></strong>, published by <strong><%= $first_node->publisher; %></strong>.</p> 

<ul>
<% 
    my $prev_level = 1;
    for my $n (@$nodes) {
        my $current_level;
        if (UNIVERSAL::isa($n, 'XFacts::Model::DropPoint')) {
            $current_level =  $n->level;
        } else {
            $current_level = $n->[0]->level;
        }
        my $l = $current_level - $prev_level; 
        my $ld = '';
        if ($l > 0) {
            $ld = '<ul>' x $l;
        } elsif ($l < 0) {
            $ld = '</ul>' x abs($l);
        }
    %>
        %== $ld
   <li>
        <% if (UNIVERSAL::isa($n, 'XFacts::Model::DropPoint')) { %>
            <%= include 'drop_point', d => $n =%>
        <% } else { %>
            <%= include 'small_medium', n => $n->[0] =%>
            <% if ($n->[1]) { %>
                <%= include 'drop_point', d => $n->[1] =%>
            <% } %>
        <% } %>
    </li>
%   $prev_level = $current_level;
% }
%== '</ul>' x $prev_level;

<script type="text/javascript">
    $('.medium').draggable({
        snap: ".drop",
        cursorAt: { top: 16, left: 16 },
        stop: function () {
//            alert('OH NOEZ, U LOSTED IT!');
        },
        helper: function (event) {
            return $( '<img src="/image/drag.png" >' );
        }
    });
    $('.drop').droppable({
        drop: function (event, ui) {
            alert('dropped ' + event + ' on ' + ui);
        }
    });
</script>

@@ medium.html.ep
<div class="node level<%= $node->level %>">
    <h2><%= $node->title %></h2>
    <% if (defined $node->made_by) { %>
        <p>Author: <%= $node->made_by %></p>
    <% } %>
    <% if ($node->id != $node->root_id) { %>
        <p>This is part of a <%= link_to "series (" . $node->root->title . ").", tree => { id => $node->root_id }; %></p>
    <% } %>
    <p>
        <% if (defined $node->medium_image) { %>
            <% if (defined $node->large_image) { %>
                <a href="<%= $node->large_image %>">
            <% } %>
                <img src="<%= $node->medium_image %>"
                    alt="Cover of <%= $node->title %>" />
            <% if (defined $node->large_image) { %>
                </a>
            <% } %>
        <% } %>
        <br />
        <a href="<%= $node->amazon_url %>">
            View on amazon US
        </a>
    </p>
</div>

@@ small_medium.html.ep
<span id="medium_<%= $n->id %>" class="medium">
    <% if ($n->small_image) { %>
        <img src="<%= $n->small_image %>" alt="" />
    <% } %>
    <%= link_to($n->title, medium => { id => $n->id }) %>
    by <%= $n->made_by %>
</span>

@@ drop_point.html.ep
<img 
    src="/image/drop-target.png" width="32" height="32"
    id="drop_<%= $d->where %>_<%= $d->id %>" class="drop"
    alt="Drag a book with your mouse and drop it here to insert it here"
>

@@ layouts/basic.html.ep
<!doctype html><html>
    <head>
        <title><%= $title %></title>
        <style type="text/css"><!--
            body {
                padding: 0px;
                margin: 0px;
            }
            #header {
                padding: 0.5em;
                margin: 0px;
                background-color: #FFFF66;
                border-bottom: #FF9900 solid 2px;
                height: 2em;
            }
            #content {
                margin: 1em;
            }
            .node {
                border: 1px dotted black;
                padding: 0.3em;
                max-width: 20em;
                margin: none;
            }
  // --></style>
    <link rel="shortcut icon" href="/favicon.ico">
    <script type="text/javascript" src="/js/jquery-1.4.3.min.js"></script>
    <script type="text/javascript" src="/js/jquery-ui-1.8.6.custom.min.js"></script>
    </head>
    <body>
        <p id="header"><%= link_to Home => 'index' %>
        <div id="content">
            <%== content %>
        </div>
    </body>
</html>


