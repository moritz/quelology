#!/usr/bin/env perl
use 5.012;
use warnings;

use Mojolicious::Lite;
use Mojolicious::Session;
use Mojo::ByteStream 'b';
use lib 'lib';
use lib '../lib';
use Quelology::Config qw(schema config);
use Carp qw(confess);
use List::MoreUtils qw/uniq/;
use utf8;

sub trim {
    my $s = $_[0];
    $s =~ s/\A\s+//;
    $s =~ s/\s+\z//;
    $s;
}

my $schema   = schema;

app->secret( config()->{web_secret} );
app->types->type(html => 'text/html; charset=utf-8');

app->helper(shelf => sub {
    my $self = shift;
    my @items = @{ $self->session->{shelf_ids} // [] };
    return b($self->render(
        items   => [map($schema->m->by_id($_), @items)],
        partial => 1,
        template => 'small-shelf',
    ));
});

get '/' =>  sub {
    my $self = shift;
    my $t = '';
    $self->render(
        threads     => [$schema->m->threads],
        singles     => [$schema->m->singles],
        template    => 'index',
        title       => 'Quelology - book sequels and prequels',
    );
} => 'index';

sub title_for {
    my $node = shift;
    my $title = sprintf '%s for "%s" by %s',
                        (shift // ($node->is_leaf ? "Details" : "Quelology")),
                        $node->title,
                        $node->made_by;
}

get '/m/:id' => sub {
    my $self = shift;
    my $node = $schema->m->by_id($self->param('id'));
    my $template = $node->is_leaf ? 'medium' : 'tree';
    $self->render(
        layout  => 'basic',
        title   => title_for($node),
        node    => $node,
        template => $template,
    );
} => 'medium';

get '/details/:id' => sub {
    my $self = shift;
    my $node = $schema->m->by_id($self->param('id'));
    $self->render(
        layout  => 'basic',
        title   => title_for($node),
        node    => $node,
        template => 'medium',
    );
} => 'details';

get '/asin/:asin' => sub {
    my $self = shift;
    my $node = eval {
        $schema->m->from_asin($self->stash('asin'));
    };
    if ($node) {
        $self->render(
            template => 'medium',
            title    => title_for($node),
            node     => $node,
            layout   => 'basic',
        );
    } else {
        $self->render(status => 404, text => 'Book not found');
    }

} => 'asin';

get '/edit/:id' => sub {
    my $self = shift;
    my $root = $schema->m ->by_id($self->param('id'));
    $self->render(
        template => 'edit',
        nodes    => [$root->thread_with_drop_points],
        title    => title_for($root, 'Edit'),
    );
};

get '/t/:id' => sub {
    my $self = shift;
    my $node = $schema->m->by_id($self->param('id'));
    $self->render(
        layout  => 'basic',
        title   => title_for($node),
        node    => $node,
    );
} => 'tree';

post '/update/:what' => sub {
    my $self = shift;
    my $what = $self->param('what');
    my %allowed = (title => 1, made_by => 1);
    die "Can't change '$what'" unless $allowed{$what};
    die "empty $what" unless $self->param($what);
    my $node = $schema->m->by_id($self->param('id'));
    $node->update({$what => trim $self->param($what)});
    # TODO: return some JSON or HTML or so...
    $self->render(
        template    => 'small_medium',
        n           => $node,
        edit        => 1,
    );
};

sub search {
    my $self = shift;
    my $keyword = trim $self->param('keyword');
    my $res = Quelology::Config::amazon->search(
                    keywords => $keyword,
                    type     => 'Books',
               );
    $self->render(
        layout  => 'basic',
        title   => "Search results for '$keyword'",
        template => 'search',
        results => [$schema->m->websearch($keyword)],
    );

}
get  '/search/:keyword' => \&search => 'search';
post '/search/'         => \&search;

$SIG{INT} = sub {
    $schema->storage->disconnect;
    exec $^X, $0, @ARGV;
};

sub render_shelf {
    my $self = shift;
    $self->render(template => 'just-shelf');
}

post '/shelf/delete'=> sub {
    my $self = shift;
    $self->session->{shelf_ids} = [];
    render_shelf($self);
};

post '/shelf/add' => sub {
    my $self = shift;
    unshift @{$self->session->{shelf_ids}}, $self->param('id');
    @{$self->session->{shelf_ids}} = uniq @{$self->session->{shelf_ids}};
    render_shelf($self);
} => 'shelf/add';

get '/shelf/small' => sub {
    my $self = shift;
    render_shelf($self);
} => 'shelf';

get '/shelf/connect' => sub {
    my $self = shift;
    my %seen;
    my @nodes;
    for my $id (@{ $self->session->{shelf_ids} // [] }) {
        next if $seen{$id};
        push @nodes, [];
        for ($schema->m->by_id($id)->thread_with_drop_points) {
            if (ref($_) eq 'ARRAY') {
                ++$seen{$_->[0]->id};
            }
            push @{$nodes[-1]}, $_;
        }
    }
    $self->render(
        template => 'shelf/connect',
        title    => 'Personal Bookshelf - extend the Quelology',
        nodes    => \@nodes,
    );
};

post '/edit' => sub {
    my $self = shift;
    my ($source_id, $target_id, $where)
            = map $self->param($_), qw/source_id target_id edit_where/;
    my $target = $schema->m->find($target_id);
    die "No target with id $target_id" unless $target;
    my $source = $schema->m->find($source_id);
    die "No source with id $source_id" unless $source;

    warn "Moving $source_id $where $target_id\n";

    my $where_to_go = $target;

    if ( $target->is_root && $where ne 'below' ) {
        my @children = $target;
        if ($where eq 'before') {
            unshift @children, $source;
        }
        elsif ($where eq 'after') {
            push @children, $source;
        } else {
            die "Don't know what to do with position '$where' (allowed: before, after, below)";
        }
        $where_to_go = $schema->m->create_root_with_children(
            { title => '<unnamed>' },
            @children,
        );
        # TODO: delete @children from session,
        # add $where_to_go->id
    }
    elsif ( $where eq 'after' ) {
        $target->attach_right_sibling($source);
        # remove $source from session
    }
    elsif ( $where eq 'before' ) {
        $target->attach_left_sibling($source);
        # remove $source from session
    }
    elsif ( $where eq 'below' ) {
        $target->attach_leftmost_child($source);
        # remove $source from session
    }
    else {
        die "Don't know what to do with position '$where' (allowed: before, after, below)";
    }
    $self->redirect_to('/shelf/connect');
};

get '/imprint' => sub {
    my $self = shift;
    $self->render(
        template => 'imprint',
        title    => 'Legal Details for Quelology',
        layout   => 'basic',
    );
};

app->start;
$schema->storage->disconnect;
1;

__DATA__

@@ index.html.ep
% layout 'basic';
<h1>Quelology - Semantic relations between books</h1>
<p>Want to find a sequel or prequel to a book? Here is the right place!</p>
<h2>List of Series</h2>
<ul>
<% for my $n (@$threads) { %>
    <li>
    %== include small_medium_noimg => n => $n
    </li>
<% } %>
</ul>
<h2>List of single nodes</h2>
<ul>
<% for my $n (@$singles) { %>
    <li>
    <%== include small_medium_noimg => n => $n %>
    </li>
<% } %>
</ul>

@@ add_shelf_link.html.ep
<span class="add_to_shelf"><a href="#" onclick="add_id_to_shelf(<%= $node->id %>); return false">Add to personal book shelf</a></span>

@@ shelf/connect.html.ep
% layout 'basic';
<h1>Personal Book Shelf - Contribute to the Quelology</h1>
<p>Use drag and drop!</p>
% for my $n (@$nodes) {
    % my $first_node = $n->[1][0];
    <h2><%= $first_node->title %></h2>
    <p>by <strong><%= $first_node->made_by; %></strong></p> 
    <%== include 'drop_tree', nodes => $n %>
% }

@@ tree.html.ep
% layout 'basic';
<h1 class="medium"><%= $node->title %></h1>
%== include 'tree-part', node => $node

@@ tree-part.html.ep
<p>by <strong><%= $node->made_by; %></strong>, published by <strong><%= $node->publisher; %></strong>.</p> 
%# <p><%= link_to('Edit this series', edit => { id => $node->id }); %></p>
<p><a href="/edit/<%= $node->id %>">Edit this series</a></p>
% if ($node->id != $node->root_id) {
    <p>This is part of a <%= link_to "Bigger series (" . $node->title . ").", tree => { id => $node->root_id }; %></p>
% }
%== include add_shelf_link => (node => $node)
%= include 'small_medium', n => $node, edit => 0
<ul>
<% my $prev_level = 1;
   for my $n ($node->descendants) {
      my $l = $n->level - $prev_level; 
      $prev_level = $n->level;
      my $ld = '';
      if ($l > 0) {
        $ld = '<ul>' x $l;
      } elsif ($l < 0) {
        $ld = '</ul>' x abs($l);
      }
    %>
        <%== $ld =%>
    <li>
        <%= include 'small_medium', n => $n, edit => 0 =%>
    </li>
<% } =%>
<%== '</ul>' x $prev_level =%>

@@ edit_form.html.ep
<form id="edit_form" name="edit_form" method="post" action="/edit">
    <input type="hidden" id="source_id" name="source_id">
    <input type="hidden" id="target_id" name="target_id">
    <input type="hidden" id="edit_where" name="edit_where">
</form>

@@ edit.html.ep
% layout 'basic';
<%
    my $first_node = $nodes->[1][0];
%>
<h1 class="root_title"><%= $first_node->title %></h1>
<p>by <strong class="root_made_by"><%= $first_node->made_by; %></strong>,
published by <span class="root_publisher"><%= $first_node->publisher; %></span>.</p> 

<%== include 'drop_tree', nodes => $nodes %>

@@ drop_tree.html.ep
<ul>
<% 
    my $prev_level = 1;
    for my $n (@$nodes) {
        my $current_level;
        if (UNIVERSAL::isa($n, 'Quelology::Model::DropPoint')) {
            $current_level =  $n->level;
        } else {
            $current_level = $n->[0]->level;
        }
        my $l = $current_level - $prev_level;
        my $ld = '';
        if ($l > 0) {
            $ld = '<ul>' x $l;
        } elsif ($l < 0) {
            $ld = '</ul>' x abs($l);
        }
    %>
        %== $ld
   <li>
        <% if (UNIVERSAL::isa($n, 'Quelology::Model::DropPoint')) { %>
            <%= include 'drop_point', d => $n =%>
        <% } else { %>
            <%= include 'small_medium', n => $n->[0], edit => 1 =%>
            <% if ($n->[1]) { %>
                <%= include 'drop_point', d => $n->[1] =%>
            <% } %>
        <% } %>
    </li>
%   $prev_level = $current_level;
% }
%== '</ul>' x $prev_level;

@@ medium.html.ep
<div class="node draggable" id="medium_<%= $node->id %>" data-id="<%= $node->id %>">
<h2><%= $node->title %></h2>
<% if (defined $node->made_by) { %>
    <p>Author: <%= $node->made_by %></p>
<% } %>
<% if ($node->id != $node->root_id) { %>
    <p>This is part of a <%= link_to "series (" . $node->root->title . ").", tree => { id => $node->root_id }; %></p>
<% } %>
%== include add_shelf_link => (node => $node)
<p>
    <% if (defined $node->medium_image) { %>
        <% if (defined $node->large_image) { %>
            <a href="<%= $node->large_image %>">
            <% } %>
                <img src="<%= $node->medium_image %>"
                    alt="Cover of <%= $node->title %>" />
            <% if (defined $node->large_image) { %>
                </a>
            <% } %>
        <% } %>
        <br />
        <a href="<%= $node->amazon_url %>">
            View on amazon US
        </a>
    </p>
</div>

@@ small_medium_noimg.html.ep
<span id="medium_<%= $n->id %>" class="medium" data-id="<%= $n->id %>">
    <span class="data_title">
    <%= link_to($n->title, medium => { id => $n->id }) %>
    </span>
    by
    <span class="data_made_by">
        <%= $n->made_by %>
    </span>
    (id <%= $n->id %>)
</span>

@@ small_medium.html.ep
% my $tree_position = $n->is_root ? 'root' : $n->is_leaf ? 'leaf' : 'middle';
<span id="medium_<%= $n->id %>" class="medium" data-treeposition="<%= $tree_position %>"
    data-id="<%= $n->id %>">
    <% if ($n->small_image) { %>
        <img src="<%= $n->small_image %>" alt="" />
    <% } %>

    <span class="data_title">
    <%= link_to($n->title, details => { id => $n->id }) %>
    %== include 'edit_marker' => edit => $edit, what => 'title', name => 'title'
    </span>
    by 
    <span class="data_made_by">
        <%= $n->made_by %>
        %== include 'edit_marker' => edit => $edit, what => 'made_by', name => 'author'
    </span>
    (id <%= $n->id %>)
</span>

@@ edit_marker.html.ep
<% if ($edit) { %>
    <a href="#" onclick="update_medium(<%= $n->id %>, '<%= $what %>'); return false">
        <img src="/image/edit.png" alt="(edit <%= $name %>)" title="edit <%= $name %>"
            width="16" height="16" />
    </a>
<% } %>

@@ drop_point.html.ep
<img
    src="/image/drop-target.png" width="32" height="32"
    id="drop_<%= $d->where %>_<%= $d->id %>" class="drop droppable"
    alt="Drag a book with your mouse and drop it here to insert it here"
>

@@ just-shelf.html.ep
%== shelf

@@ small-shelf.html.ep
<div id="shelf">
    % if (@$items) {
        <ul>
    %   for my $i (@$items) {
            <li class="draggable" id="shelfmedium_<%= $i->id %>" data-id="<%= $i->id %>">
            <%= link_to medium => { id => $i->id } => (title => $i->title) => begin %>
                <%= $i->short_title %>
            <% end %></li>
    %   }
        </ul>
    % } else {
        <p id="placeholder">Personal book shelf - drag books here.</p>
    %}
    <a href="/shelf/connect">Connect books on your shelf</a>
    <a href="#" onclick="clear_shelf()" style="clear: left">Clear bookshelf</a>
</div>

@@ search.html.ep
<ul>
    % for (@$results) {
        <li>
            <%= include small_medium  => (n => $_, edit => 0) %>
            <%= include 'add_shelf_link', node => $_ =%>
        </li>
    % }
</ul>

@@ imprint.html.ep
<h1>Legal Details</h1>
<p>Responsible for this site in general is</p>
<p>
Moritz Lenz <br />
Sperlingstr. 2 <br />
91056 Erlange <br />
Germany
</p>
<p>Please note that much of the content is user-generated,
and belongs to whoever generated it. Each author is
responsible for whatever he writes.</p>

@@ layouts/basic.html.ep
<!doctype html><html>
    <head>
        <title><%= $title %></title>
        <style type="text/css"><!--
            body {
                padding: 0px;
                margin: 0px;
                height: 100%;
            }
            .shelf-droppable, #footer {
                background-color: #FFFF66;
            }
            #header {
                padding: 0.5em;
                margin: 0px;
                min-height: 2em;
            }
            #left-column {
                position: absolute;
                width: 2em;
                height: 100%;
            }
            #content {
                margin: 1em;
                margin-left: 3em;
            }
            #footer {
                padding: 0.3em;
                margin-left: 2em;
            }
            .node {
                border: 1px dotted black;
                padding: 0.3em;
                max-width: 20em;
                margin: none;
            }
            .data_made_by {
                font-weight: bold;
            }
            #shelf {
                padding: 0.1em;
            }
            #shelf li {
                float: left; 
                list-style-type: none;
                padding: 1ex;
            }
  // --></style>
    <link rel="shortcut icon" href="/favicon.ico">
    <script type="text/javascript" src="/js/jquery-1.4.3.min.js"></script>
    <script type="text/javascript" src="/js/jquery-ui-1.8.6.custom.min.js"></script>
    <script type="text/javascript" src="/js/custom.js"></script>
    </head>
    <body>
        <%== include 'edit_form' %>
        <div id="header" class="shelf-droppable droppable">
            <p style="display: inline;"><%= link_to Home => 'index' %> </p>
            <form action="/search/" method="post">
            <p style="float:right"><input type="text" id="keyword" name="keyword" />
               <input type="submit" value="search" />
            </p>
            </form>
            <%== shelf %>
        </div>
        <div id="left-column" class="shelf-droppable droppable">
        </div>
        <div id="content">
            <%== content %>
        </div>
    <div id="footer">
        <a href="/imprint">Legal Details</a>
    </div>
    </body>
</html>
